
-------------  プログラム0の中身　LRUN0で呼び出し  ---------
POKE#A58,4,4,4,4,4:POKE#A38,4,4,4,4,4:LC6,10:?"SUCCESS":END
------------------------------------------------------------

/* 初期値設定 */
5 CLS:A=1:B=1:C=1:H=3:Z=0:S=0:X=9:F=1

/* ジャム瓶描画 */
7 FORY=0TO23:LC21,Y:?CHR$(7):NEXT
8 FORY=8TO15:LC23,Y:?CHR$(1):LC29,Y:?CHR$(1):NEXT
9 POKE#AF8,1,1,1,1,1:POKE#A18,1:POKE#A1C,1:POKE#9F8,1:POKE#9FC,1:POKE#9D7,1,1:POKE#9DC,1,1

/* HP描画 */
10 IFH=3LC22,0:?"HP:***"
11 IFH=2LC27,0:?" "
12 IFH=1LC26,0:?" "
13 IFH=0LC25,0:?" ":LC5,10:?"GAME OVER":END

/* 矢印キー入力読み取り */
14 K=INKEY()

/* K=28=[←] K=29=[→] 移動可能なら移動 */
15 IF K=28 AND X<>3 THEN LCX-1,22:?CHR$(0,0,0):X=X-6
16 IF K=29 AND X<>15 THEN LCX-1,22:?CHR$(0,0,0):X=X+6

/* 自機描画 */
17 LCX-1,22:?CHR$(141,140,142)

/* イチゴの数に応じて描画 */
20 IFS>=3POKE#AD8,4,4,4,4,4:POKE#AB8,4,4,4,4,4
21 IFS>=6ANDS<=8POKE#A98,4,4,4,4,4:POKE#A78,4,4,4,4,4
22 IFS=9 LRUN0 //どうしてもメモリ足りなかったので…なんとか1つに納めれるよう工夫します.

/* イチゴ落下　もしイチゴがしたまでいけば、初期位置に戻してHPを0か1(Fによる)減らす */
25 IF(Z%2)=0ANDA<>22THENLC3,A:?CHR$(255):A=A+1ELSEIFA=22THENA=1:LC3,21:?CHR$(0):H=H-F:F=1
30 IF(Z%5)=0ANDB<>22THENLC9,B:?CHR$(255):B=B+1ELSEIFB=22THENB=1:LC9,21:?CHR$(0):H=H-F:F=1
35 IF(Z%3)=0ANDC<>22THENLC15,C:?CHR$(255):C=C+1ELSEIFC=22THENC=1:LC15,21:?CHR$(0):H=H-F:F=1

/* 自機の上の文字コードを読み取る */
36 R=SCR(X,21)

/* もし自機の上がイチゴだったら次のループで25~35の間にHPを減らさないように */
37 IF R=255 THEN S=S+1:F=0:LCX,21:?CHR$(0)
38 LC3,A-2:?CHR$(0):LC9,B-2:?CHR$(0):LC15,C-2:?CHR$(0)
39 Z=Z+1
46 GOTO10

--以下実行用--

5 CLS:A=1:B=1:C=1:H=3:Z=0:S=0:X=9:F=1
7 FORY=0TO23:LC21,Y:?CHR$(7):NEXT
8 FORY=8TO15:LC23,Y:?CHR$(1):LC29,Y:?CHR$(1):NEXT
9 POKE#AF8,1,1,1,1,1:POKE#A18,1:POKE#A1C,1:POKE#9F8,1:POKE#9FC,1:POKE#9D7,1,1:POKE#9DC,1,1
10 IFH=3LC22,0:?"HP:***"
11 IFH=2LC27,0:?" "
12 IFH=1LC26,0:?" "
13 IFH=0LC25,0:?" ":LC5,10:?"GAME OVER":END
14 K=INKEY()
15 IF K=28 AND X<>3 THEN LCX-1,22:?CHR$(0,0,0):X=X-6
16 IF K=29 AND X<>15 THEN LCX-1,22:?CHR$(0,0,0):X=X+6
17 LCX-1,22:?CHR$(141,140,142)
20 IFS>=3POKE#AD8,4,4,4,4,4:POKE#AB8,4,4,4,4,4
21 IFS>=6ANDS<=8POKE#A98,4,4,4,4,4:POKE#A78,4,4,4,4,4
22 IFS=9 LRUN0
25 IF(Z%2)=0ANDA<>22THENLC3,A:?CHR$(255):A=A+1ELSEIFA=22THENA=1:LC3,21:?CHR$(0):H=H-F:F=1
30 IF(Z%5)=0ANDB<>22THENLC9,B:?CHR$(255):B=B+1ELSEIFB=22THENB=1:LC9,21:?CHR$(0):H=H-F:F=1
35 IF(Z%3)=0ANDC<>22THENLC15,C:?CHR$(255):C=C+1ELSEIFC=22THENC=1:LC15,21:?CHR$(0):H=H-F:F=1
36 R=SCR(X,21)
38 LC3,A-2:?CHR$(0):LC9,B-2:?CHR$(0):LC15,C-2:?CHR$(0)
37 IF R=255 THEN S=S+1:F=0:LCX,21:?CHR$(0)
39 Z=Z+1
46 GOTO10
