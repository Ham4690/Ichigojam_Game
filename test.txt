1 'インベーダーゲーム
10 CLV:VIDEO3:P=80:X=7
20 @H:CLS:?"SCORE:";S:A=#95F:B=A-1:C=-48:D=1:E=3:F=#930:G=F+1:H=47:M=#96F:N=M-16:O=-64:J=0:Z=0:P=P-10:T=0:FORI=0TO17:LCI%6*2,I/6+E:?CHR$(237):NEXT
30 @A:IFTLC2,4:?"C L E A R !!":WAIT120:GOTO@H
40 IFSCR((D=1)*14,E)=237||SCR((D=1)*14,E+1)=237||SCR((D=1)*14,E+2)=237GSB@D:GOTO@B
50 IFD=1GSB@RELSEGSB@L
60 @B:K=INKEY():IFKLCX,10:?" "
70 X=X-(K=28)*(X>0)+(K=29)*(X<14):LCX,10:?CHR$(240):IFK=32&&!ZY=10:W=X:Z=1
80 IF!ZGOTO@C
90 IFVGOTO@E
100 GSB@F:Y=Y-1:IF!YZ=0:GOTO@C
110 GSB@G
120 @E:V=V-1:IFV<0V=10
130 @C:L=L-1:IFL<0L=P:GOTO@A
140 GOTO@B
150 @R:GSB@F:COPYA,B,C:GSB@G:RTN
160 @L:GSB@F:COPYF,G,H:GSB@G:RTN
170 @D:GSB@F:COPYM,N,O:GSB@G:FORI=0TO14:J=J+(SCR(I,10)=237):NEXT:IFJLC0,1:?"GAME OVER":END
180 E=E+1:A=A+16:B=B+16:F=F+16:G=G+16:M=M+16:N=N+16:D=-D:IFE>7A=A-16:B=B-16:C=C+16:H=H-16:M=M-16:N=N-16:O=O+16
190 RTN
200 @F:IFZLCW,Y:?" "
210 RTN
220 @G:LCW,Y:IFZ&&SCR(W,Y)=237?CHR$(244):Z=0:S=S+10:LC0,0:?"SCORE:";S:WAIT15:LCW,Y:?" ":T=(S%180=0):RTN
230 IFZ?CHR$(242)
240 RTN